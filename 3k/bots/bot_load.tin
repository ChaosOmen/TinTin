#class {botload} {kill}
#class {botload} {open}

#NOP Global Bot Variable Initialization
#var mobname {}

#NOP Load specific stepper file
#alias {- %1} {
	#var no_home 0;
	#var no_loop 0;
	#var bots[stepper] %1;
	#read .tt/3k/bots/%1.tin;
	#read .tt/3k/bots/bot_main.tin
}

#NOP Resume a stepper after a disconnect DisConnectResume
#alias {.dcr} {
        - $bots[stepper];
        #delay {path_position} {#path goto $bots[position]} {0.5};
	#map run $bots[vnum];
        #delay {stepper_start} {..} {2}
}

#NOP Party Variable
#var party {}

#alias pa {#list party add %0}
#alias pr {
        #list party find %0 party_index;
        #list party delete $party_index
}

#action {^Name         Location                                  Creator$} {
	#action {^%w %+ (%w%s)} {
		#var {party_member} {%%1};
		#var party_found 0;
		#list party find $party_member party_found;
		#if {$party_found == 0} {
			#list party add $party_member
		}
	} {1};
	#delay {party_capture} {
		#unaction {^%w %+ (%w%s)};
	} {0.5}
}

#NOP Low priority aliases to mute output from mud
#alias {.bot%*} {#showme !bot is not active} {9}
#alias {.stop%*} {#showme !Bot is not active} {9}
#alias {.resume%*} {#showme !Bot is not active} {9}
#alias {killbot%*} {#showme !Bot is not active} {9}

#class {botload} {close}
