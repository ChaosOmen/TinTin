#class {corpses} {open}

/* Initialize vars */
#var inv_count 0
#var coffin_count 0
#var cooler_count 0
#var freezer_count 0
#var truck_count 0

/* Coffin Counter */
#act {{You keep An enchanted coffin \((\d+) corpses?\) \[(?:[\.\|\&]+)\] \[[\w\s]+\]\.}} {#var coffin_count %2}
#act {{wrap it up into the coffin\'s protective hold!}} {#math {coffin_count} {$coffin_count + 1}}
#act {{The coffin expels (a|your selected) corpse onto the ground!}} {
	#if {$coffin_count > 0} {
		#math {coffin_count} {$coffin_count - 1}
	} {
		#var {coffin_count} {0}
	}
}

/* Freezer Counter */
#act {{You keep \*?Death\'s Freezer \((\d+) corpses\)\.}} {#var freezer_count %2}
#act {{frame causing it to get sucked in!}} {#math {freezer_count} {$freezer_count + 1}}
#act {{You shake the frame and out drops }} {
	#if {$freezer_count > 0} {
		#math {freezer_count} {$freezer_count - 1}
	} {
		#var {freezer_count} {0}
	}
}

/* Tech Ultra-Cooler Counter */
#act {{The Tech Ultra-Cooler contains:}} {#var cooler_count 0;corpsecount cooler}
#act {{The cooler does not contain a corpse\.}} {#var cooler_count 0}
#act {{There is no reason to \'uncooler\' here\.}} {
	#var cooler off;
	#var cooler_count 0
}
#act {{There is no more room in the Ultra-Cooler}} {#var cooler_count 50}
#act {{You place the corpse into the Ultra-Cooler and seal the lid}} {
	#var cooler on;
	#math {cooler_count} {$cooler_count + 1};
	#if {$inv_count > 0} {
		#math {inv_count} {$inv_count - 1}
	} {
		#var inv_count 0
	}
}
#act {{You activate the Ultra-Cooler to retrieve }} {#math {cooler_count} {$cooler_count - 1}}

/* Inventory Counter */
#act {{You keep (?:.*) corpse of }} {#math {inv_count} {$inv_count + 1}}
#act {{You drop (?:.*) corpse of }} {
	#if {$inv_count > 0} {
		#math {inv_count} {$inv_count - 1}
	} {
		#var inv_count 0
	}
}
#alias {kac} {#var inv_count 0;!keep all corpse}
	
/* Truck Counter */
#act {{^A tank named CanisLupus}} {#var truck_count 0;corpsecount truck}
#act {{Canislupus takes: (?:.*) corpse of }} {#math {truck_count} {$truck_count + 1}}
#act {{Canislupus drops (?:.*)[corpse|preservation|remains]}} {
	#if {$truck_count > 0} {
		#math {truck_count} {$truck_count - 1}
	}
}

/* Corpse Regex */
#alias corpsecount {
	#class {corpse_count} {open};
	#var {corpse_cnt} {0};
	#act {{^[\[|\(](?:.*) corpse of (?:[\w\s\d\,\-]+)\{?(\d*)\}?(\: Ok)?\.}} { 
		#if {!%%3} {
			#if {%%2} {
				#var corpse_stack %%2
			} {
				#var corpse_stack 1
			};
			#math {corpse_cnt} {$corpse_cnt + $corpse_stack}
		}
	};
	#act {^>$} {
		#var {%1_count} {$corpse_cnt};
		#class {corpse_count} {kill}
	};
	#class {corpse_count} {close}
}

/* Aliases */
#alias {cc} {
	kac;
	!keep coffin;
	!keep freezer;
	!exa cl;
	!exa cooler
}
#class {corpses} {close}
